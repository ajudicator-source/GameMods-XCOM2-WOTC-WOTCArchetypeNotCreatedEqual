
/// Class X2DownloadableContentInfo_WOTCArchetypeNotCreatedEqual_TierResolver
/// Handles tier-based stat value resolution for the Archetype Not Created Equal mod.
/// Contains static functions for resolving soldier stats by weighted tier selection,
/// generating tier ranges, and calculating maximum high-tier stats allowed per soldier.
class X2DownloadableContentInfo_WOTCArchetypeNotCreatedEqual_TierResolver extends X2DownloadableContentInfo config(ATNCE);

/// Function: ATNCE_ResolveStatValueByTierWeight
/// Purpose: Resolves a stat value based on selected tier weighting and tier range configurations.
/// Params:
///   tierRanges - The tier range definitions (min/max values for each tier)
///   minRefineTier - The minimum tier that should be refined (prevents lower tiers from being selected)
///   soldierDetail - Internal soldier stat state and archetype information
///   enableLogging - Whether to enable debug logging
///   selectedTier - Output parameter containing the selected tier type
/// Returns: The resolved stat value based on the selected tier's range
static function int ATNCE_ResolveStatValueByTierWeight(
	ATNCE_TierRanges tierRanges,
    ATNCE_TierType selectedTier,
	bool enableLogging)
{
	local int setStatValue, rangeDiff;

	switch (selectedTier)
    {
        case ATNCE_TierC:
            rangeDiff = tierRanges.TierCHigh - tierRanges.TierCLow + 1;
            setStatValue = tierRanges.TierCLow + `SYNC_RAND_STATIC(Max(1, rangeDiff));
            break;
        case ATNCE_TierB:
            rangeDiff = tierRanges.TierBHigh - tierRanges.TierBLow + 1;
            setStatValue = tierRanges.TierBLow + `SYNC_RAND_STATIC(Max(1, rangeDiff));
            break;
        case ATNCE_TierA:
            rangeDiff = tierRanges.TierAHigh - tierRanges.TierALow + 1;
            setStatValue = tierRanges.TierALow + `SYNC_RAND_STATIC(Max(1, rangeDiff));
            break;
        default:
            rangeDiff = tierRanges.TierDHigh - tierRanges.TierDLow + 1;
            setStatValue = tierRanges.TierDLow + `SYNC_RAND_STATIC(Max(1, rangeDiff));
            break;
    }

	return setStatValue;
}

/// Function: ATNCE_SelectTierByWeighting
/// Purpose: Selects a tier (D, C, B, or A) based on weighted probability distribution.
/// Adjusts weights based on archetype bonuses and high-tier stat limits.
/// Params:
///   statType - The character stat type being evaluated
///   weights - The weight configuration for each tier
///   minRefineTier - The minimum tier that should be refined (prevents lower tiers from being selected)
///   soldierDetail - Internal soldier stat state and archetype information
/// Returns: The selected tier type (ATNCE_TierA, B, C, or D)
static function ATNCE_TierType ATNCE_SelectTierByWeighting(
	ATNCE_StatConfig statConfig,
	ATNCE_SelectTierRanges selectTierRanges, 
	ATNCE_SoldierDetail soldierDetail)
{
    local int setWeightD, setWeightC, setWeightB, setWeightA;
    local int totalWeight, roll, cumulative;
    local ATNCE_CoreConfig coreConfig;
    local int currentHighStatCount;

    coreConfig = class'X2DownloadableContentInfo_WOTCArchetypeNotCreatedEqual'.static.ATNCE_GetCoreConfig();

    currentHighStatCount = soldierDetail.HighTierStatsCount;

    setWeightD = statConfig.TierWeights.WeightD;
    setWeightC = statConfig.TierWeights.WeightC;
    setWeightB = statConfig.TierWeights.WeightB;
    setWeightA = statConfig.TierWeights.WeightA;

    `LOG("Input Tier Values: D=" @ setWeightD @ "C=" @ setWeightC @ "B=" @ setWeightB @ "A=" @ setWeightA, coreConfig.ATNCE_EnableLogging, 'WOTCArchetype_ATNCE');

    if(soldierDetail.SelectedArchetypeIndex < 0
        && soldierDetail.PrimaryStatRequiresHighTier
        && (currentHighStatCount + 1) == soldierDetail.MaxHighTierStatsAllowed
        && statConfig.StatGroupType != ATNCE_Primary)
    {
        currentHighStatCount = soldierDetail.MaxHighTierStatsAllowed;
    }

    if (selectTierRanges.minSelectTier > ATNCE_TierD || selectTierRanges.maxSelectTier < ATNCE_TierD) setWeightD = 0;
    if (selectTierRanges.minSelectTier > ATNCE_TierC || selectTierRanges.maxSelectTier < ATNCE_TierC) setWeightC = 0;
    if (selectTierRanges.minSelectTier > ATNCE_TierB || selectTierRanges.maxSelectTier < ATNCE_TierB) setWeightB = 0;
    if (selectTierRanges.minSelectTier > ATNCE_TierA || selectTierRanges.maxSelectTier < ATNCE_TierA) setWeightA = 0;

    if (soldierDetail.SelectedArchetypeIndex >= 0)
    {
        if (statConfig.CharStatType == soldierDetail.ArchetypeStatConfig.primaryCharStatType)
        {
            setWeightD = 0; 
            setWeightC = 0;
        }
        else if (statConfig.CharStatType == soldierDetail.ArchetypeStatConfig.secondaryCharStatType)
        {
            setWeightD = 0;
        }
    }
    else if(soldierDetail.PrimaryStatRequiresHighTier && statConfig.StatGroupType == ATNCE_Primary)
    {
        return ATNCE_TierB;
    }

    if (currentHighStatCount >= soldierDetail.MaxHighTierStatsAllowed)
    {   
        setWeightA = 0;
        setWeightB = 0;
        setWeightC = statConfig.TierWeights.WeightC;
        setWeightD = statConfig.TierWeights.WeightD;
    }   

    `LOG("Resolved Tier Ranges: Min" @ selectTierRanges.minSelectTier @ "to" @ selectTierRanges.maxSelectTier, coreConfig.ATNCE_EnableLogging, 'WOTCArchetype_ATNCE');
    `LOG("Resolved Tier Values: D=" @ setWeightD @ "C=" @ setWeightC @ "B=" @ setWeightB @ "A=" @ setWeightA, coreConfig.ATNCE_EnableLogging, 'WOTCArchetype_ATNCE');

    totalWeight = setWeightD + setWeightC + setWeightB + setWeightA;

    if (totalWeight <= 0)
    {
        if (currentHighStatCount >= soldierDetail.MaxHighTierStatsAllowed && selectTierRanges.maxSelectTier >= ATNCE_TierC)
        {
            return ATNCE_TierC;
        }
        return selectTierRanges.minSelectTier;
    }

    roll = `SYNC_RAND_STATIC(totalWeight);
    cumulative = 0;

    cumulative += setWeightD;
    if (roll < cumulative) return ATNCE_TierD;

    cumulative += setWeightC;
    if (roll < cumulative) return ATNCE_TierC;

    cumulative += setWeightB;
    if (roll < cumulative) return ATNCE_TierB;

    return ATNCE_TierA;
}

/// Function: ATNCE_GenerateTierRangesByArchetype
/// Purpose: Generates tier range (min/max values) for each tier based on stat configuration.
/// Divides the stat range into four tiers with configurable overlap regions.
/// Applies adjustments to prevent narrow ranges and overlapping tier boundaries.
/// Params:
///   config - The stat configuration containing base stat ranges and tier parameters
/// Returns: ATNCE_TierRanges structure with min/max values for tiers D, C, B, and A
static function ATNCE_TierRanges ATNCE_GenerateTierRangesByArchetype(const ATNCE_StatConfig statConfig)
{
	local ATNCE_TierRanges returnTierRanges;
    local float midLow, midHigh;
    local float baseMins[4], baseMaxs[4], tierSizes[4];
    local float useShifts[3];
    local int tier;
    local int outMins[4], outMaxs[4];
	local bool bIsNarrowLowerHalf;
	local int prevMax, thisMin, overlapSize;
    local ATNCE_CoreConfig coreConfig;

    coreConfig = class'X2DownloadableContentInfo_WOTCArchetypeNotCreatedEqual'.static.ATNCE_GetCoreConfig();

    useShifts[0] = coreConfig.ATNCE_TierMaxOverlaps.DtoCPercent / 100.0f;
    useShifts[1] = coreConfig.ATNCE_TierMaxOverlaps.CtoBPercent / 100.0f;
    useShifts[2] = coreConfig.ATNCE_TierMaxOverlaps.BtoAPercent / 100.0f;

    midLow = (statConfig.StatRanges.RangeLow + statConfig.StatRanges.RangeMid) / 2.0f;
    midHigh = (statConfig.StatRanges.RangeMid + statConfig.StatRanges.RangeHigh) / 2.0f;

    baseMins[0] = statConfig.StatRanges.RangeLow;
    baseMaxs[0] = midLow;
    baseMins[1] = midLow;
    baseMaxs[1] = statConfig.StatRanges.RangeMid;
    baseMins[2] = statConfig.StatRanges.RangeMid;
    baseMaxs[2] = midHigh;
    baseMins[3] = midHigh;
    baseMaxs[3] = statConfig.StatRanges.RangeHigh;

    for (tier = 0; tier < 4; tier++)
    {
        tierSizes[tier] = baseMaxs[tier] - baseMins[tier];
    }

    for (tier = 0; tier < 3; tier++)
    {
        baseMaxs[tier] += useShifts[tier] * tierSizes[tier + 1];
    }

    for (tier = 0; tier < 4; tier++)
    {
        outMins[tier] = Round(baseMins[tier]);
        outMaxs[tier] = Round(baseMaxs[tier]);

		if (outMins[tier] < statConfig.StatRanges.RangeLow)
		{
			outMins[tier] = statConfig.StatRanges.RangeLow;
		}
    }

	bIsNarrowLowerHalf = (statConfig.StatRanges.RangeMid - statConfig.StatRanges.RangeLow) <= 5;

	if (bIsNarrowLowerHalf)
	{
		for (tier = 1; tier < 4; tier++)
		{
			prevMax = outMaxs[tier-1];
			thisMin  = outMins[tier];

			overlapSize = prevMax - thisMin + 1;
			if (thisMin <= prevMax && overlapSize >= 1)
			{
				outMins[tier] = Min(outMaxs[tier], prevMax + 1);
				if (outMins[tier] > outMaxs[tier])
				{
					outMins[tier] = outMaxs[tier];
				}
			}
			else if (thisMin == prevMax && `SYNC_RAND_STATIC(2) == 0)
			{
				outMins[tier] = Min(outMaxs[tier], prevMax + 1);
				if (outMins[tier] > outMaxs[tier])
				{
					outMins[tier] = outMaxs[tier];
				}
			}
		}
	}

    returnTierRanges.TierDLow = outMins[0];
    returnTierRanges.TierDHigh = outMaxs[0];
    returnTierRanges.TierCLow = outMins[1];
    returnTierRanges.TierCHigh = outMaxs[1];
    returnTierRanges.TierBLow = outMins[2];
    returnTierRanges.TierBHigh = outMaxs[2];
    returnTierRanges.TierALow = outMins[3];
    returnTierRanges.TierAHigh = outMaxs[3];

	if (returnTierRanges.TierAHigh > statConfig.StatRanges.RangeHigh)
	{
		returnTierRanges.TierAHigh = statConfig.StatRanges.RangeHigh;
	}

	return returnTierRanges;
}

/// Function: ATNCE_CalculateMaxHighTierStatsAllowed
/// Purpose: Calculates the high-tier stat cap using fixed percentages.
/// Guarantees a "Hero" floor of at least 2 to 4 high-tier stats, while
/// Returns: An integer representing the randomized high-tier stat cap.
static function int ATNCE_CalculateMaxHighTierStatsAllowed()
{
    local int arrayLen;
    local int minResult;
    local int maxResult;
    local int randomRange;
    local ATNCE_CoreConfig coreConfig;

    coreConfig = class'X2DownloadableContentInfo_WOTCArchetypeNotCreatedEqual'.static.ATNCE_GetCoreConfig();
    arrayLen = coreConfig.ATNCE_StatTierWeights.Length;
    minResult = 2;
    maxResult = 3;

    if(arrayLen < 7) return minResult;

    if(arrayLen == 7) return maxResult;

    if(arrayLen >= 10)
    {
        minResult = 3;
        maxResult = 4;
    }
    
    randomRange = (maxResult - minResult) + 1;
    return minResult + `SYNC_RAND_STATIC(randomRange);
}

